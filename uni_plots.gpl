set terminal pngcairo size 800,582 enhanced font 'Verdana,10'

set border linewidth 1.1
set style line 1 lc rgb '#0060ad' lt 1 lw 2 pt 7 ps 0.3 # --- blue
unset key

set tics scale 0.75
set xlabel 'Effective connectance'

set output "sim_stats.png"
set multiplot layout 2, 2

set ylabel 'Variance'
plot "unipartites.dat" u 1:4 ls 1

set ylabel 'Coefficient of variation'
plot "unipartites.dat" u 1:(sqrt($4)/$3) ls 1

set ylabel 'Kurtosis'
plot "unipartites.dat" u 1:5 ls 1

set ylabel 'Skewness'
plot "unipartites.dat" u 1:6 ls 1

unset multiplot

set terminal pngcairo size 600,382 enhanced font 'Verdana,10'

set output 'sim_power.png'
set ylabel 'Power-law exponent'
plot "unipartites.dat" u 1:7 ls 1

set output 'sim_modul.png'
set ylabel 'Louvain modularity'
plot "unipartites.dat" u 1:8 ls 1
